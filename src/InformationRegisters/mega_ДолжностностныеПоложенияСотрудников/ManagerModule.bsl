
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ПрограммныйИнтерфейс

// Получить мастеров цеха.
// 
// Параметры:
//  Подразделение - СправочникСсылка.mega_Подразделения - Подразделение/Цех
//  ДатаСреза - Дата, Неопределено - Дата среза
// 
// Возвращаемое значение:
//  Массив Из СправочникСсылка.Пользователи - Мастера цеха
Функция ПолучитьМастеровЦеха(Подразделение, ДатаСреза = Неопределено) Экспорт
	
	Должность = Константы.mega_ДолжностьМастерЦеха.Получить();
	Возврат ПолучитьРаботниковЦехаДолжности(Подразделение, Должность, ДатаСреза)
КонецФункции

// Получить начальников цеха.
// 
// Параметры:
//  Подразделение - СправочникСсылка.mega_Подразделения - Подразделение/Цех
//  ДатаСреза - Дата, Неопределено - Дата среза
// 
// Возвращаемое значение:
//  Массив Из СправочникСсылка.Пользователи - Начальники цеха
Функция ПолучитьНачальниковЦеха(Подразделение, ДатаСреза = Неопределено) Экспорт
	
	Должность = Константы.mega_ДолжностьНачальникЦеха.Получить();
	Возврат ПолучитьРаботниковЦехаДолжности(Подразделение, Должность, ДатаСреза)
КонецФункции


Функция ПолучитьСотрудниковПодразделения(Подразделение, ДатаСреза = Неопределено) Экспорт
	
	Если НЕ ЗначениеЗаполнено(ДатаСреза) Тогда
		
		ДатаСреза = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	mega_ДолжностностныеПоложенияСотрудниковСрезПоследних.Сотрудник,
		|	mega_ДолжностностныеПоложенияСотрудниковСрезПоследних.Должность
		|ИЗ
		|	РегистрСведений.mega_ДолжностностныеПоложенияСотрудников.СрезПоследних(&ДатаСреза,
		|		Подразделение = &Подразделение) КАК mega_ДолжностностныеПоложенияСотрудниковСрезПоследних";
	
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("ДатаСреза", ДатаСреза);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Возврат ВыборкаДетальныеЗаписи;
КонецФункции

#КонецОбласти

#Область ОбработчикиСобытий

// Код процедур и функций

#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс

// Код процедур и функций

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьРаботниковЦехаДолжности(Подразделение, Должность, ДатаСреза = Неопределено)
	
	Если НЕ ЗначениеЗаполнено(ДатаСреза) Тогда
		
		ДатаСреза = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	mega_ДолжностностныеПоложенияСотрудниковСрезПоследних.Сотрудник
		|ИЗ
		|	РегистрСведений.mega_ДолжностностныеПоложенияСотрудников.СрезПоследних(&ДатаСреза, Подразделение = &Подразделение
		|	И Должность = &Должность) КАК mega_ДолжностностныеПоложенияСотрудниковСрезПоследних";
	
	Запрос.УстановитьПараметр("Подразделение", Подразделение);
	Запрос.УстановитьПараметр("Должность", Должность);
	Запрос.УстановитьПараметр("ДатаСреза", ДатаСреза);
	
	РезультатЗапроса = Запрос.Выполнить();
	Возврат РезультатЗапроса.Выгрузить().ВыгрузитьКолонку("Сотрудник");
КонецФункции

#КонецОбласти

#КонецЕсли
