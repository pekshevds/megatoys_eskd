///////////////////////////////////////////////////////////////////////////////////////////////////////
// Copyright (c) 2023, ООО 1С-Софт
// Все права защищены. Эта программа и сопроводительные материалы предоставляются 
// в соответствии с условиями лицензии Attribution 4.0 International (CC BY 4.0)
// Текст лицензии доступен по ссылке:
// https://creativecommons.org/licenses/by/4.0/legalcode
///////////////////////////////////////////////////////////////////////////////////////////////////////

#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОбработчикиСобытий

Процедура ОбработкаЗаполнения(ДанныеЗаполнения, ТекстЗаполнения, СтандартнаяОбработка)
	
	Если ОбменДанными.Загрузка Тогда
		
		Возврат;
	КонецЕсли;  
	
	ДатаСоздания = ТекущаяДатаСеанса();
	Автор = Пользователи.ТекущийПользователь();
	
	Если ТипЗнч(ДанныеЗаполнения) = Тип("СправочникСсылка.mega_ШаблоныСпецификаций") Тогда
		
		 ЗаполнитьПоШаблонуСпецификации(ДанныеЗаполнения);		
	КонецЕсли;
КонецПроцедуры

Процедура ПередЗаписью(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		
		Возврат;
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(ДатаСоздания) Тогда
	
		ДатаСоздания = ТекущаяДатаСеанса();
	КонецЕсли;
	
	Если НЕ ЗначениеЗаполнено(Автор) Тогда
	
		Автор = Пользователи.ТекущийПользователь();
	КонецЕсли;
	
	ЕстьСопутствующийВыпуск = СопутствующийВыпуск.Количество() > 0;
КонецПроцедуры

Процедура ПриЗаписи(Отказ)
	
	Если ОбменДанными.Загрузка Тогда
		
		Возврат;
	КонецЕсли;
	
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	mega_СпецификацииСтадииОбработки.ВидСтадииОбработки КАК ВидСтадииОбработки,
	|	КОЛИЧЕСТВО(РАЗЛИЧНЫЕ mega_СпецификацииСтадииОбработки.ВидСтадииОбработки) КАК Количество
	|ИЗ
	|	Справочник.mega_Спецификации.СтадииОбработки КАК mega_СпецификацииСтадииОбработки
	|ГДЕ
	|	mega_СпецификацииСтадииОбработки.Ссылка = &Спецификация
	|
	|СГРУППИРОВАТЬ ПО
	|	mega_СпецификацииСтадииОбработки.ВидСтадииОбработки
	|
	|УПОРЯДОЧИТЬ ПО
	|	Количество УБЫВ";
	
	Запрос.УстановитьПараметр("Спецификация", ЭтотОбъект.Ссылка);
	
	Результат = Запрос.Выполнить();
	Выборка = Результат.Выбрать();
	
	Если Выборка.Следующий() Тогда 
	
		Если Выборка.Количество > 1 Тогда 
			
			ОбщегоНазначения.СообщитьПользователю("Не допустимо использовать одинаковые стадии обработки",,,, Отказ);
		КонецЕсли;
	КонецЕсли;	
КонецПроцедуры

Процедура ПриКопировании(ОбъектКопирования)
	
	ДатаСоздания = Неопределено;
	Автор = Неопределено;
	Владелец = Неопределено;
КонецПроцедуры

#КонецОбласти
	
#Область СлужебныеПроцедурыИФункции

Функция ПолучитьВыборкуПолейШаблонаСпецификации(ШаблонСпецификации)
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
	"ВЫБРАТЬ
	|	mega_ШаблоныСпецификаций.Наименование КАК Наименование,
	|	mega_ШаблоныСпецификаций.Количество КАК Количество,
	|	mega_ШаблоныСпецификаций.Материалы.(
	|		Ссылка КАК Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		Номенклатура КАК Номенклатура,
	|		Количество КАК Количество,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		Спецификация КАК Спецификация
	|	) КАК Материалы,
	|	mega_ШаблоныСпецификаций.ТехнологическиеОперации.(
	|		Ссылка КАК Ссылка,
	|		НомерСтроки КАК НомерСтроки,
	|		Номенклатура КАК Номенклатура,
	|		Количество КАК Количество,
	|		ЕдиницаИзмерения КАК ЕдиницаИзмерения,
	|		ВремяВыполнения КАК ВремяВыполнения
	|	) КАК ТехнологическиеОперации
	|ИЗ
	|	Справочник.mega_ШаблоныСпецификаций КАК mega_ШаблоныСпецификаций
	|ГДЕ
	|	mega_ШаблоныСпецификаций.Ссылка = &ШаблонСпецификации";
	
	Запрос.УстановитьПараметр("ШаблонСпецификации", ШаблонСпецификации);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Возврат ВыборкаДетальныеЗаписи;
КонецФункции

Процедура ЗаполнитьПоШаблонуСпецификации(ШаблонСпецификации)
	
	ВыборкаДетальныеЗаписи = ПолучитьВыборкуПолейШаблонаСпецификации(ШаблонСпецификации);
	
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		Наименование = ВыборкаДетальныеЗаписи.Наименование;
		Количество = ВыборкаДетальныеЗаписи.Количество;
		
		Материалы.Очистить();
		
		ВыборкаМатериалы = ВыборкаДетальныеЗаписи.Материалы.Выбрать();
		Пока ВыборкаМатериалы.Следующий() Цикл
			
			НоваяСтрока = Материалы.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаМатериалы);
		КонецЦикла;
		
		ТехнологическиеОперации.Очистить();
		
		ВыборкаТехнологическиеОперации = ВыборкаДетальныеЗаписи.ТехнологическиеОперации.Выбрать();
		Пока ВыборкаТехнологическиеОперации.Следующий() Цикл
			
			НоваяСтрока = ТехнологическиеОперации.Добавить();
			ЗаполнитьЗначенияСвойств(НоваяСтрока, ВыборкаТехнологическиеОперации);
		КонецЦикла;
		
	КонецЦикла;
КонецПроцедуры

#КонецОбласти

#Иначе
ВызватьИсключение НСтр("ru = 'Недопустимый вызов объекта на клиенте.'");
#КонецЕсли