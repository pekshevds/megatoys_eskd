#Область ОбработчикиСобытий

Функция testGET(Запрос)
	//http://localhost:8080/lib2/hs/exchange/api/v1/test
	
	Ответ = Новый HTTPСервисОтвет(200);	
	Ответ.УстановитьТелоИзСтроки("hello world");
	
	Возврат Ответ;
КонецФункции

Функция specificationsGET(Запрос)
	//http://localhost:8080/lib2/hs/exchange/api/v1/specifications
	
	Ответ = Новый HTTPСервисОтвет(200);
	
	Данные = Новый Структура();
	Данные.Вставить("data", ПолучитьДанныеСпецификаций());
	
	ЗаписьJSON = Новый ЗаписьJSON();
	ЗаписьJSON.УстановитьСтроку();	
	ЗаписатьJSON(ЗаписьJSON, Данные);
	
	Ответ.УстановитьТелоИзСтроки(ЗаписьJSON.Закрыть(), "utf-8");
	
	Возврат Ответ;
КонецФункции

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ПолучитьСтруктуруСпецификации()
	
	Структура = Новый Структура();
	Структура.Вставить("specification_id", "");
	Структура.Вставить("specification_name", "");
	Структура.Вставить("stage_id", "");
	Структура.Вставить("stage_name", "");
	Структура.Вставить("index", 0);
	Структура.Вставить("qnt", 0);
	Возврат Структура;
КонецФункции

Функция ПолучитьДанныеСпецификаций()
		
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	Спецификации.ВидСтадииОбработки КАК stage,
		|	Спецификации.ВидСтадииОбработки.Наименование КАК stage_name,
		|	Спецификации.Количество КАК qnt,
		|	Спецификации.Ссылка.Наименование КАК specification_name,
		|	Спецификации.НомерСтроки КАК index,
		|	Спецификации.Ссылка КАК specification
		|ИЗ
		|	Справочник.mega_Спецификации.СтадииОбработки КАК Спецификации
		|ГДЕ
		|	НЕ Спецификации.Ссылка.ПометкаУдаления
		|
		|УПОРЯДОЧИТЬ ПО
		|	specification,
		|	index";
	
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	
	Данные = Новый Массив();
	Пока ВыборкаДетальныеЗаписи.Следующий() Цикл
		
		СтруктураСпецификации = ПолучитьСтруктуруСпецификации();
		ЗаполнитьЗначенияСвойств(СтруктураСпецификации, ВыборкаДетальныеЗаписи);
		СтруктураСпецификации.specification_id = XMLСтрока(ВыборкаДетальныеЗаписи.specification.УникальныйИдентификатор());
		СтруктураСпецификации.stage_id = XMLСтрока(ВыборкаДетальныеЗаписи.stage.УникальныйИдентификатор());
		Данные.Добавить(СтруктураСпецификации);
	КонецЦикла;
	
	Возврат Данные;
КонецФункции

#КонецОбласти
