
#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаКлиенте
Процедура ОбработкаВыбора(ВыбранноеЗначение, ИсточникВыбора)

	Если ИсточникВыбора.ИмяФормы = "Справочник.mega_Спецификации.Форма.ФормаВыбора" И 
		ЗначениеЗаполнено(ВыбранноеЗначение) Тогда
			
		ДобавитьСпецификациюНаСервере(ВыбранноеЗначение);		
		РазвернутьДерево();	
	КонецЕсли; 
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

// Код процедур и функций

#КонецОбласти

#Область ОбработчикиСобытийЭлементовТаблицыФормы //<ИмяТаблицыФормы>

// Код процедур и функций

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура ДобавитьСпецификацию(Команда)
	
	ПараметрыОткрытия = Новый Структура;
	ПараметрыОткрытия.Вставить("ЗакрыватьПриВыборе", Ложь);
	ПараметрыОткрытия.Вставить("МножественныйВыбор", Ложь);
	ОткрытьФорму("Справочник.mega_Спецификации.Форма.ФормаВыбора", ПараметрыОткрытия, ЭтотОбъект,,,,, РежимОткрытияОкнаФормы.БлокироватьОкноВладельца);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура РазвернутьДерево()
	
	КоллекцияЭлементов = СтруктураСпецификации.ПолучитьЭлементы();
	Для Каждого Строка Из КоллекцияЭлементов Цикл
			
    	ИдентификаторСтроки = Строка.ПолучитьИдентификатор();
    	Элементы.СтруктураСпецификации.Развернуть(ИдентификаторСтроки);
    КонецЦикла;
КонецПроцедуры

&НаСервере
Функция Обновить(Параметры)
	
КонецФункции

&НаСервере
Процедура ДобавитьСпецификациюНаСервере(Спецификация)
	
	ДеревоЗначений = РеквизитФормыВЗначение("СтруктураСпецификации");
	
	ТаблицаДляПолученияОстатков = ИнициализацияТаблицыДляПолученияОстатков();
	
	НоваяСтрокаНоменклатура = ДеревоЗначений.Строки.Добавить();
	НоваяСтрокаНоменклатура.Номенклатура = ОбщегоНазначения.ЗначениеРеквизитаОбъекта(Спецификация, "Владелец");
	НоваяСтрокаНоменклатура.СтадияОбработки = Спецификация; 
	
	ДобавитьВТаблицуДляПолученияОстатков(ТаблицаДляПолученияОстатков, 
		НоваяСтрокаНоменклатура.Номенклатура, Справочники.mega_ВидыСтадийОбработки.ПустаяСсылка());
		
	ВыборкаМатериалов = Справочники.mega_Спецификации.ПолучитьМатериалыСпецификации(Спецификация, 1);
	ВыборкаСтадийОбработки = Справочники.mega_Спецификации.ПолучитьСтадииОбработкиСпецификации(Спецификация);	

	Пока ВыборкаСтадийОбработки.Следующий() Цикл
		
		НоваяСтрокаСтадияОбработки = НоваяСтрокаНоменклатура.Строки.Добавить();
		НоваяСтрокаСтадияОбработки.Номенклатура = ВыборкаСтадийОбработки.ВидСтадииОбработки;
		
		ДобавитьВТаблицуДляПолученияОстатков(ТаблицаДляПолученияОстатков, 
			НоваяСтрокаНоменклатура.Номенклатура, НоваяСтрокаСтадияОбработки.Номенклатура);
				
		ВыборкаМатериалов.Сбросить();
		Пока ВыборкаМатериалов.НайтиСледующий(Новый Структура("КлючСвязи", ВыборкаСтадийОбработки.КлючСвязи)) Цикл
			
			Если ЗначениеЗаполнено(ВыборкаМатериалов.СтадияОбработки) Тогда
				
				НоваяСтрокаМетериал = НоваяСтрокаСтадияОбработки.Строки.Добавить();
				НоваяСтрокаМетериал.Номенклатура = ВыборкаМатериалов.Номенклатура;
				НоваяСтрокаМетериал.СтадияОбработки = ВыборкаМатериалов.СтадияОбработки;
				
				ДобавитьВТаблицуДляПолученияОстатков(ТаблицаДляПолученияОстатков, 
					НоваяСтрокаМетериал.Номенклатура, НоваяСтрокаМетериал.СтадияОбработки);
			КонецЕсли;
		КонецЦикла;
	КонецЦикла;	
	
	ОстаткиНоменклатуры = ОстаткиНоменклатуры(ТаблицаДляПолученияОстатков);
	Если ОстаткиНоменклатуры.НайтиСледующий(
		Новый Структура("Номенклатура, ВидСтадииОбработки", 
			НоваяСтрокаНоменклатура.Номенклатура, Справочники.mega_ВидыСтадийОбработки.ПустаяСсылка())) Тогда
		
		НоваяСтрокаНоменклатура.НаСкладе = ОстаткиНоменклатуры.НаСкладе;
		НоваяСтрокаНоменклатура.ВПроизводстве = ОстаткиНоменклатуры.ВПроизводстве;
		
		Для Каждого НоваяСтрокаСтадияОбработки Из НоваяСтрокаНоменклатура.Строки Цикл
			
			ОстаткиНоменклатуры.Сбросить();
			Если ОстаткиНоменклатуры.НайтиСледующий(
				Новый Структура("Номенклатура, ВидСтадииОбработки", 
					НоваяСтрокаНоменклатура.Номенклатура, НоваяСтрокаСтадияОбработки.Номенклатура)) Тогда
				НоваяСтрокаСтадияОбработки.НаСкладе = ОстаткиНоменклатуры.НаСкладе;
				НоваяСтрокаСтадияОбработки.ВПроизводстве = ОстаткиНоменклатуры.ВПроизводстве;	
			КонецЕсли;
			
			Для Каждого НоваяСтрокаМетериал Из НоваяСтрокаСтадияОбработки.Строки Цикл
				
				ОстаткиНоменклатуры.Сбросить();
				Если ОстаткиНоменклатуры.НайтиСледующий(
					Новый Структура("Номенклатура, ВидСтадииОбработки", 
						НоваяСтрокаМетериал.Номенклатура, НоваяСтрокаМетериал.СтадияОбработки)) Тогда
					НоваяСтрокаМетериал.НаСкладе = ОстаткиНоменклатуры.НаСкладе;
					НоваяСтрокаМетериал.ВПроизводстве = ОстаткиНоменклатуры.ВПроизводстве;	
				КонецЕсли;
			КонецЦикла;
		КонецЦикла;
	КонецЕсли;
	
	ЗначениеВРеквизитФормы(ДеревоЗначений, "СтруктураСпецификации");
КонецПроцедуры

&НаСервере
Функция ИнициализацияТаблицыДляПолученияОстатков()
	
	ТаблицаЗначений = Новый ТаблицаЗначений;
	ТаблицаЗначений.Колонки.Добавить("Номенклатура", Новый ОписаниеТипов("СправочникСсылка.mega_Номенклатура"));
	ТаблицаЗначений.Колонки.Добавить("ВидСтадииОбработки", Новый ОписаниеТипов("СправочникСсылка.mega_ВидыСтадийОбработки"));
	
	Возврат ТаблицаЗначений;
КонецФункции

&НаСервере
Процедура ДобавитьВТаблицуДляПолученияОстатков(ТаблицаЗначений, Номенклатура, ВидСтадииОбработки)
	
	Если ТаблицаЗначений.НайтиСтроки(
		Новый Структура("Номенклатура,ВидСтадииОбработки", Номенклатура, ВидСтадииОбработки)).Количество() > 0 Тогда
		
		Возврат;
	КонецЕсли;
	
	НоваяСтрока = ТаблицаЗначений.Добавить();
	НоваяСтрока.Номенклатура = Номенклатура;
	НоваяСтрока.ВидСтадииОбработки = ВидСтадииОбработки;		
КонецПроцедуры

&НаСервере
Функция ОстаткиНоменклатуры(ТаблицаДляПолученияОстатков)
	
	
	Запрос = Новый Запрос;
	Запрос.Текст =
		"ВЫБРАТЬ
		|	ТаблицаДляПолученияОстатков.Номенклатура,
		|	ТаблицаДляПолученияОстатков.ВидСтадииОбработки
		|ПОМЕСТИТЬ втТаблицаДляПолученияОстатков
		|ИЗ
		|	&ТаблицаДляПолученияОстатков КАК ТаблицаДляПолученияОстатков
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	втТаблицаДляПолученияОстатков.Номенклатура,
		|	втТаблицаДляПолученияОстатков.ВидСтадииОбработки,
		|	СУММА(ЕСТЬNULL(ПроизводственныеЗапасыОстатки.КоличествоОстаток, 0)) КАК ВПроизводстве,
		|	СУММА(ЕСТЬNULL(СкладскиеЗапасыОстатки.КоличествоОстаток, 0)) КАК НаСкладе
		|ИЗ
		|	втТаблицаДляПолученияОстатков КАК втТаблицаДляПолученияОстатков
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.mega_ПроизводственныеЗапасы.Остатки КАК ПроизводственныеЗапасыОстатки
		|		ПО втТаблицаДляПолученияОстатков.Номенклатура = ПроизводственныеЗапасыОстатки.Номенклатура
		|		И втТаблицаДляПолученияОстатков.ВидСтадииОбработки = ПроизводственныеЗапасыОстатки.ВидСтадииОбработки
		|		ЛЕВОЕ СОЕДИНЕНИЕ РегистрНакопления.mega_СкладскиеЗапасы.Остатки КАК СкладскиеЗапасыОстатки
		|		ПО втТаблицаДляПолученияОстатков.Номенклатура = СкладскиеЗапасыОстатки.Номенклатура
		|		И втТаблицаДляПолученияОстатков.ВидСтадииОбработки = СкладскиеЗапасыОстатки.ВидСтадииОбработки
		|СГРУППИРОВАТЬ ПО
		|	втТаблицаДляПолученияОстатков.Номенклатура,
		|	втТаблицаДляПолученияОстатков.ВидСтадииОбработки";
	
	Запрос.УстановитьПараметр("ТаблицаДляПолученияОстатков", ТаблицаДляПолученияОстатков);
	РезультатЗапроса = Запрос.Выполнить();
	
	ВыборкаДетальныеЗаписи = РезультатЗапроса.Выбрать();
	Возврат ВыборкаДетальныеЗаписи;	
КонецФункции

#КонецОбласти
